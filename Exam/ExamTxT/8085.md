# Askisi 1

Να κατασκευαστεί χρονόμετρο δευτερολέπτων που θα απεικονίζει το χρόνο σε δυαδική μορφή πάνω στα LEDs εξόδου του mLab. Θεωρήστε ότι το αναμμένο των LED αντιστοιχεί σε λογικό ‘1’ και το σβηστό στο λογικό ‘0’ του μετρητή. Για την υλοποίηση της χρονοκαθυστέρησης του 1 sec μπορείτε να χρησιμοποιήσετε την έτοιμη ρουτίνα DELB που υπάρχει στο παράρτημα 1 του βιβλίο των σημειώσεων του εργαστηρίου http://mycourses.ntua.gr/courses/ECE1175/document/MLAB_8085- 80x86.pdf. Το χρονόμετρο όταν φτάνει στην τιμή x[0...15] που καθορίζεται από τις τιμές των 4ων δεξιότερων (LSB) της θύρας 2000 Hex να αρχίζει την μέτρηση προς τα κάτω έως το 0 και μετά προς τα πάνω συνεχόμενα. Τα παραπάνω με την προϋπόθεση το MSB των διακοπτών να είναι ΟΝ. Αλλιώς η μέτρηση να σταματάει μέχρι να ξανα-ενεργοποιηθεί ο MSB διακόπτης.
Διευκρινίζεται ότι για την περίπτωση που αλλάζουν τιμή οι 4 LSB διακόπτες στην διάρκεια της μέτρησης, να θεωρήσετε ότι το x αποθηκεύει την τιμή του όταν περνάει από το ‘0’ και κρατάει την τιμή αυτή σταθερή μέχρι να ξαναπεράσει από το ‘0’.

```asm
start:
    	IN 10H
    	LXI B,03E8H     ;BC -> 1000dec (1ms*1000 = 1s)

count_up:
    	LDA 2000H	; DIAVAZW TA DIP-SWITCHES
    	ANI 0FH		; PETAW TA 4-MSB
	   CPI 00H		; AN EINAI OLA SVISTA PERIMENE MEXRI NA ANAPSEI KAPOIO
	   JZ count_up
    	MOV D,A    	; KAI APOTHIKEYW STO D
    	MVI A,FFH		; VAZW STO A TO FF ETSI WSTE +1 NA DINEI 0
cont_up:
    	MOV E,A		; APOTHIKEYW STO E THN CURRENT TIMH TOU METRITH
lock_up:
    	LDA 2000H     ; DIAVAZW KSANA TA DIP-SWITCHES 
    	ANI 80H		; KRATAW TO MSB
    	CPI 80H		; ELEGXW AN EINAI 1
    	JNZ lock_up	; AN DEN EINAI KSANA ELEGXW
    	MOV A,E		; ALLIWS VAZW STO A TIMH TOY METRITH
    	INR A		; AYKSANW THN TIMH TOY METRITH
    	CMA        
    	STA 3000H		; CMA LOGO ARNITIKIS LOGIKIS STOUS DIAKOPTES
    	CALL DELB
    	CMA
    	CMP D		; SIGKRINW ME TON ARITHMO POU DOTHIKE STA 4 LSB DIP SWITCHES
    	JZ count_down	; OTAN FTASEI STON ARITHMO KSEKINAEI ANTISTROFI METRISI
    	JMP cont_up

count_down:
    	MOV E,A
lock_down:
    	LDA 2000H		; CHECK FOR MSB
    	ANI 80H
    	CPI 80H
    	JNZ lock_down
    	MOV A,E
    	DCR A
    	CMA
    	STA 3000H
    	CMA
    	CPI 00H		; ELEGXO AN EFTASE STO 0
    	JZ count_up	; ALLAZW SE COUNT UP 	
	   CALL DELB		; DEN KALEITAI KATHISTERISI GIA TO 0 GIATI GINETAI STO COUNT UP
    	JMP count_down	; ALLIWS SINEXISE TIN ANTISTROFI METRISI

END
```

# Askisi 2

Γράψτε πρόγραμμα που να εισάγει από το πληκτρολόγιο δύο δεκαεξαδικούς αριθμούς x,y {0-F}. Στη συνέχεια να απεικονίζει στα τρία δεξιότερα 7-segment display την ποσότητα 16∙x+y σε δεκαδική μορφή τριών ψηφίων. Η διαδικασία να είναι συνεχόμενη δηλαδή κάθε φορά μετά από την εισαγωγή 2 HEX αριθμών να ανανεώνεται η οθόνη.

```asm
IN 10H
START:
	CALL	KIND	; FERE APO TO PLIKTROLOGIO
	CPI 	10H	; AN A>0x0F
	JNC	START	; PARE NEO NUM
	RLC		; x2
	RLC		; x4
	RLC		; x8
	RLC		; x16
	MOV	B,A	; GRAPSE TON A STO B
FERE_Y:
	CALL	KIND	; FERE KAI TON EPOMENO	
	CPI	10H	
	JNC	FERE_Y 
	ADD	B	; A = 16x + y
	MVI	B,00H	; MHDENIZW TON B
	MVI	C,00H	; MHDENIZW TON C
EKATONTADES:
	CPI	64H	; SYGKRINW ME TO 100
	JC	DEKADES ; AN EINAI MIKROTERO EKSETAZW TO 10
	INR	C	; PROSTHETO 1 STON DEIKTH TWN EKATONTADWN
	SUI	64H	; AFAIRW 100
	JMP	EKATONTADES
DEKADES:
	CPI	0AH	; SYGKRINW ME TO 10
	JC	DONE	; AN EINAI MIKROTERO TELEIWSA
	INR	B	; PROSTHETW ALLO ENA
	SUI	0AH	; AFAIRW 10
	JMP	DEKADES
DONE:	
	STA	0A00H	; GRAPSE TO PSIFIO TWN MONADWN
	MOV	A,B	; BALE TIS DEKADES STON ACUM
	STA	0A01H	; GRAPSE TO PSIFIO TWN DEKADWN
	MOV	A,C	; OMOIWS ME EKATONTADES
	STA	0A02H
	LXI	D,0A00H	;FORTONO STO ZEVGOS D-E TIN THESI MNIMIS POU ARXIZOUN TA SEGMENTS
	CALL	STDM	 ;METAKINEI TO MINIMA POU KSEKINA STIN PARAPANW DIEUTHINSI STIN 
	CALL	DCD   ;DIEUTHINSI POU TO PERIPENEI I DCD, DCD PAROUSIAZEI STA SEGMENTS
	JMP	START 
END
```

# Askisi 3

Να εξομοιωθεί ένας αυτοματισμός βαγονέτου που κινείται αρχικά από δεξιά προς τα αριστερά (LSB => MSB) και στη συνέχεια αντίστροφα συνεχώς, με την προϋπόθεση ότι είναι ON ο διακόπτης LSB (1ος) της θύρας εισόδου 20H. Αν οποιαδήποτε στιγμή ο διακόπτης αυτός γίνει OFF το βαγονέτο να αλλάζει κατεύθυνση. Το βαγονέτο να φαίνεται στη μορφή ενός led (της θύρα εξόδου 30Η) και η κίνησή του θα γίνεται κατά μία θέση κάθε ~ 0,5 sec. Το βαγονέτο θα ξεκινάει από την μία άκρη και θα καταλήγει στην άλλη όπου θα αναστρέφεται η κίνηση αφού κάνει μια πρόσθετη στάση 0,5 sec (συνολικά στα άκρα θα παραμένει 1 sec). Η κίνηση του βαγονέτου θα ελέγχεται από τον MSB διακόπτη (8ο) της θύρας εισόδου 20H. Όταν αυτό είναι ON το βαγονέτο κινείται, όταν είναι OFF να σταματάει στη θέση που είναι και να “θυμάται” την κατεύθυνσή του όταν ο MSB διακόπτης επανέλθει στη θέση ΟΝ.

```asm
IN 10H
LXI	D,0101H		; D -> FLAG 0b1 = LEFT & E -> LSB DIP-SWITCH
MVI	B,FEH			; THESH TOY VAGONETOY
MOV	A,B			; ARXIKOPOIW TA LED NA DEIXNOYN TH THESI TOY VAGONETOY
STA	3000H			
CALL	MYDELAY
START:
	PUSH	D		; APOTHILKEYSE TON D KAI TON E (DIPLO KATAXORITI)
FAIL:	
	LDA	2000H	; FORTWSE TA DIP-SWITCHES STO A
	MOV	E,A
	RLC			; FERE TO MSB STO LSB
	MVI	D,01H	; VALE MASKA T0 0b1
	ANA	D		 
	CMP	D		; AN LOIPON TO MSB EINAI ANOIKTO PROXWRA
	JNZ	FAIL
	MOV	A,E		; EPANAFERE TIMI TWN SWITCHES
	ANA	D		; VALE PALI TH MASKA 0x1 STON A
	POP	D		; EPISTREPSE TON D KAI TON E 
	CMP	E		; E = 01H
	JZ	CONT		; AN EINAI ISA SHMAINEI DEN ALLAKSE TO LSB ARA SYNEXIZW
	CALL	CHANGE_DIR	; ROUTINA GIA ALLAGH KATEYUYNSHS
	MOV	A,E		; ALLAZW THN TIMH TOU DIP-SWITCH
	XRI	01H		; 
	MOV	E,A		; APOTHIKEYW
	
CONT:		
	CALL	CHECK_EDGE	; PRIN KSEKINHSW ELEGXW AN EIMAI STHN AKRH
	MOV	A,D		; ALLIWS KOITAZW THN FORA TOY VAGONETOY
	CPI	01H		; ELEGXW
	JNZ	GO_RIGHT	; AN EINAI D=0x1 ARISTERA ALLIWS DEKSIA
	MOV	A,B		; VAZW THN CURRENT THESI TOY STON A
	CALL	MOVL		; KAI KANW ARISTERA
	JMP	START	; EPISTREFW
GO_RIGHT:
	CALL	MOVR		; OMOIWS DEKSIA
	JMP	START

CHECK_EDGE:
	MOV	A,B		; PAIRNW TH THESH
	CPI	7FH		; AN EINAI TERMA ARISTERA
	JNZ	RIGHT_CHECK
	MOV	A,D		; PERNW THN FORA TOY
	CPI	01H		; AN !!KAI!! H FORA EINAI ARISTERA  
	JNZ	NO_EDGE	;
	CALL	CHANGE_DIR	; ALLAZW KATEYTHYNSH
	CALL	MYDELAY	; VAZW KAI TO EXTRA DELAY
	RET			
RIGHT_CHECK:		; ALLIWS KANW TOYS IDIOYS ELEGXOYS GIA DEKSIA
	CPI	FEH		; EINAI TERMA DEKSIA ???
	JNZ	NO_EDGE
	MOV	A,D
	CPI	00H		; EXEI FORA PROS TA DEKSIA ???
	JNZ	NO_EDGE
	CALL	CHANGE_DIR
	CALL	MYDELAY
	RET
NO_EDGE:			; ALLIWS APLA SYNEXIZW
	RET

CHANGE_DIR:		; ROUTINA ALLAGHS FORAS
	MOV	A,D		; PAIRNW TH THESH MNHMHS POY EXW TH FORA
	XRI	01H		; THN ANTISTREFW
	MOV	D,A		; APOTHIKEYW
	RET
MOVL:			; ROUTINA KINHSHS ARISTERA
	MOV	A,B		; PAIRNW THESH VAGONETOU
	RLC			; METAKINW ARISTERA
	STA	3000H	; EMFANIZW STA LED
	CALL	MYDELAY	; VAZW KATHISTERISH
	MOV	B,A		; KAI TELOS APOTHIKEYW PISW
	RET
MOVR:			; OMOIWS ROUTINA GIA KINHSH DEKSIA
	MOV	A,B
	RRC
	STA	3000H
	CALL	MYDELAY
	MOV	B,A
	RET
MYDELAY:			; ROUTINA GIA KATHISTERISH 0.5 SEC
	PUSH	B
	LXI	B,01F4H	; FORTWNW STO B-C POSO XRONO THELW 
	CALL	DELB		; KAI TREXW GNWSTH ROUTINA
	POP	B
	RET
END	

```