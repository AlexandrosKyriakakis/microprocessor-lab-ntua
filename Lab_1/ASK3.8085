IN 10H
LXI	D,0101H		; D -> FLAG 0b1 = LEFT & E -> LSB DIP-SWITCH
MVI	B,FEH			; THESH TOY VAGONETOY
MOV	A,B			; ARXIKOPOIW TA LED NA DEIXNOYN TH THESI TOY VAGONETOY
STA	3000H			
CALL	MYDELAY
START:
	PUSH	D		; APOTHILKEYSE TON D
FAIL:	
	LDA	2000H		; FORTWSE TA DIP-SWITCHES STO A
	RLC			; FERE TO MSB STO LSB
	MVI	D,01H		; VALE MASKA T0 0b1
	ANA	D		; 
	CMP	D		; AN LOIPON TO MSB EINAI ANOIKTO PROXWRA
	JNZ	FAIL
	LDA	2000H		; KSANA FORTWSE GIA NA DOYME TO LSB
	ANA	D		; VALE PALI TH MASKA 0x1 STON A
	POP	D		; EPISTREPSE TON D
	CMP	E		; E = 01H
	JZ	CONT		; AN EINAI ISA SHMAINEI DEN ALLAKSE TO LSB ARA SYNEXIZW
	CALL	CHANGE_DIR	; ROUTINA GIA ALLAGH KATEYUYNSHS
	MOV	A,E		; ALLAZW THN TIMH TOU DIP-SWITCH
	XRI	01H		; 
	MOV	E,A		; APOTHIKEYW
	
CONT:		
	CALL	CHECK_EDGE	; PRIN KSEKINHSW ELEGXW AN EIMAI STHN AKRH
	MOV	A,D		; ALLIWS KOITAZW THN FORA TOY VAGONETOY
	CPI	01H		; ELEGXW
	JNZ	GO_RIGHT	; AN EINAI D=0x1 ARISTERA ALLIWS DEKSIA
	MOV	A,B		; VAZW THN CURRENT THESI TOY STON A
	CALL	MOVL		; KAI KANW ARISTERA
	JMP	START		; EPISTREFW
GO_RIGHT:
	CALL	MOVR		; OMOIWS DEKSIA
	JMP	START

CHECK_EDGE:
	MOV	A,B		; PAIRNW TH THESH
	CPI	7FH		; AN EINAI TERMA ARISTERA
	JNZ	RIGHT_CHECK
	MOV	A,D		; PERNW THN FORA TOY
	CPI	01H		; AN !!KAI!! H FORA EINAI ARISTERA  
	JNZ	RIGHT_CHECK	;
	CALL	CHANGE_DIR	; ALLAZW KATEYTHYNSH
	CALL	MYDELAY	; VAZW KAI TO EXTRA DELAY
	RET			
RIGHT_CHECK:		; ALLIWS KANW TOYS IDIOYS ELEGXOYS GIA DEKSIA
	CPI	FEH		; EINAI TERMA DEKSIA ???
	JNZ	NO_EDGE
	MOV	A,D
	CPI	00H		; EXEI FORA PROS TA DEKSIA ???
	JNZ	NO_EDGE
	CALL	CHANGE_DIR
	CALL	MYDELAY
	RET
NO_EDGE:			; ALLIWS APLA SYNEXIZW
	RET

CHANGE_DIR:			; ROUTINA ALLAGHS FORAS
	MOV	A,D		; PAIRNW TH THESH MNHMHS POY EXW TH FORA
	XRI	01H		; THN ANTISTREFW
	MOV	D,A		; APOTHIKEYW
	RET
MOVL:				; ROUTINA KINHSHS ARISTERA
	MOV	A,B		; PAIRNW THESH VAGONETOU
	RLC			; METAKINW ARISTERA
	STA	3000H		; EMFANIZW STA LED
	CALL	MYDELAY	; VAZW KATHISTERISH
	MOV	B,A		; KAI TELOS APOTHIKEYW PISW
	RET
MOVR:				; OMOIWS ROUTINA GIA KINHSH DEKSIA
	MOV	A,B
	RRC
	STA	3000H
	CALL	MYDELAY
	MOV	B,A
	RET
MYDELAY:			; ROUTINA GIA KATHISTERISH 0.5 SEC
	PUSH	B
	LXI	B,1FFFH	; FORTWNW STO B-C POSO XRONO THELW 
	CALL	DELB		; KAI TREXW GNWSTH ROUTINA
	POP	B
	RET
END	
